{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "28f17190-fa87-42c9-be56-82e4dc51e8e0",
   "metadata": {},
   "source": [
    "# CIS 194: Homework 1"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "77d3d3aa-3e88-45e6-9320-71df39e1b191",
   "metadata": {},
   "source": [
    "## Validating Credit Card Numbers\n",
    "\n",
    "In this section, you will implement the validation algorithm for\n",
    "credit cards. It follows these steps:\n",
    "- Double the value of every second digit beginning from the right.\n",
    "That is, the last digit is unchanged; the second-to-last digit is doubled; the third-to-last digit is unchanged; and so on. For example,\n",
    "[1,3,8,6] becomes [2,3,16,6].\n",
    "- Add the digits of the doubled values and the undoubled digits from the original number. For example, [2,3,16,6] becomes\n",
    "2+3+1+6+6 = 18.\n",
    "- Calculate the remainder when the sum is divided by 10. For the\n",
    "above example, the remainder would be 8.\n",
    "\n",
    "If the result equals 0, then the number is valid."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "c7cba810-ac62-4efe-9070-ba726cb7d795",
   "metadata": {},
   "outputs": [],
   "source": [
    "toDigitsRev :: Integer -> [Integer]\n",
    "toDigitsRev x\n",
    "  | x > 0 = x `mod` 10 : toDigitsRev (x `div` 10)\n",
    "  | otherwise = []\n",
    "\n",
    "toDigits :: Integer -> [Integer]\n",
    "toDigits = reverse . toDigitsRev\n",
    "\n",
    "doubleEveryOther :: [Integer] -> [Integer]\n",
    "doubleEveryOther = reverse . zipWith (*) (cycle [1, 2]) . reverse\n",
    "\n",
    "sumDigits :: [Integer] -> Integer\n",
    "sumDigits = sum . concatMap toDigits\n",
    "\n",
    "validate :: Integer -> Bool\n",
    "validate = (== 0) . (`mod` 10) . sumDigits . doubleEveryOther . toDigits"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "id": "3e540afb-99c9-4dc2-b7f6-6f48361ad972",
   "metadata": {
    "scrolled": true
   },
   "outputs": [
    {
     "data": {
      "text/plain": [
       "True"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "validate 4012888888881881"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "id": "ccae920c-3165-4ea3-93b5-f032538de4fc",
   "metadata": {
    "scrolled": true
   },
   "outputs": [
    {
     "data": {
      "text/plain": [
       "False"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "validate 4012888888881882"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "15da52ff-53c4-4b03-8906-6a9630b4fdcb",
   "metadata": {
    "scrolled": true
   },
   "source": [
    "## The Towers of Hanoi\n",
    "Given the number of discs and names for the three pegs, hanoi\n",
    "should return a list of moves to be performed to move the stack of\n",
    "discs from the first peg to the second."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "id": "21f1c118-a98d-4971-92d2-d09d946364ca",
   "metadata": {},
   "outputs": [],
   "source": [
    "type Peg = String\n",
    "\n",
    "type Move = (Peg, Peg)\n",
    "\n",
    "hanoi :: Int -> Peg -> Peg -> Peg -> [Move]\n",
    "hanoi 1 a b c = [(a, b)]\n",
    "hanoi n a b c = hanoi (n - 1) a c b ++ (a, b) : hanoi (n - 1) c b a"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "id": "b8e363a8-6da9-4703-8edf-07c7cdd138ad",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "[(\"a\",\"b\")]\n",
       "[(\"a\",\"c\"),(\"a\",\"b\"),(\"c\",\"b\")]\n",
       "[(\"a\",\"b\"),(\"a\",\"c\"),(\"b\",\"c\"),(\"a\",\"b\"),(\"c\",\"a\"),(\"c\",\"b\"),(\"a\",\"b\")]"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "ns = [1..3]\n",
    "f x = hanoi x \"a\" \"b\" \"c\"\n",
    "mapM_ (print . f) ns"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "d093400a-1085-44bd-8281-a2f0146b75e3",
   "metadata": {},
   "source": [
    "### Optional\n",
    "What if there are four pegs instead of three?\n",
    "That is, the goal is still to move a stack of discs from the first peg to\n",
    "the last peg, without ever placing a larger disc on top of a smaller\n",
    "one, but now there are two extra pegs that can be used as “temporary” storage instead of only one. Write a function similar to hanoi\n",
    "which solves this problem in as few moves as possible."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "2fb91b22-0726-4fd0-99eb-27bd5289d386",
   "metadata": {},
   "source": [
    "- 使用Frame-Stewart算法\n",
    "- 需注意四柱汉诺塔的最优解法至今没有严格的数学证明，但Frame-Stewart算法被广泛认为是最优的"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 6,
   "id": "7441874a-5bb3-45c1-b009-504a32572204",
   "metadata": {},
   "outputs": [],
   "source": [
    "import Data.List (minimumBy)\n",
    "import Data.Ord (comparing)\n",
    "\n",
    "-- 计算最优的 k 值（分割点）\n",
    "-- 使用动态规划思想找到使总步数最少的 k\n",
    "optimalK :: Int -> Int\n",
    "optimalK n = fst $ minimumBy (comparing snd) candidates\n",
    "  where\n",
    "    candidates = [(k, totalMoves n k) | k <- [1 .. n - 1]]\n",
    "    totalMoves n k = 2 * t4 k + t3 (n - k)\n",
    "    -- 四柱汉诺塔的步数（递归）\n",
    "    t4 1 = 1\n",
    "    t4 m = minimum [2 * t4 i + t3 (m - i) | i <- [1 .. m - 1]]\n",
    "    -- 三柱汉诺塔的步数\n",
    "    t3 m = 2 ^ m - 1"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 7,
   "id": "b0c10f8b-da54-43b5-9c47-505802fc0944",
   "metadata": {},
   "outputs": [],
   "source": [
    "hanoi4 :: Int -> Peg -> Peg -> Peg -> Peg -> [Move]\n",
    "hanoi4 0 _ _ _ _ = []\n",
    "hanoi4 1 source target _ _ = [(source, target)]\n",
    "hanoi4 n source target aux1 aux2 =\n",
    "  let k = optimalK n\n",
    "      -- 步骤1: 将前 k 个盘子移到 aux1（使用所有四个柱子）\n",
    "      moves1 = hanoi4 k source aux1 aux2 target\n",
    "      -- 步骤2: 将剩余 n-k 个盘子移到 target（只用三个柱子）\n",
    "      moves2 = hanoi (n - k) source target aux2\n",
    "      -- 步骤3: 将 k 个盘子从 aux1 移到 target（使用所有四个柱子）\n",
    "      moves3 = hanoi4 k aux1 target aux2 source\n",
    "   in moves1 ++ moves2 ++ moves3"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 10,
   "id": "2d5764c3-29c8-45c7-9de0-1af9867f5a32",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "True"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "(==129) . length $ hanoi4 15 \"a\" \"b\" \"c\" \"d\""
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "41611f01-789e-45d9-8646-af2079b9010b",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Haskell",
   "language": "haskell",
   "name": "haskell"
  },
  "language_info": {
   "codemirror_mode": "ihaskell",
   "file_extension": ".hs",
   "mimetype": "text/x-haskell",
   "name": "haskell",
   "pygments_lexer": "Haskell",
   "version": "9.6.7"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
