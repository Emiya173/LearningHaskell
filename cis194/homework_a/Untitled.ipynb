{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "17e80ab0-d9d8-4fa1-b9c5-1cffd3726619",
   "metadata": {},
   "source": [
    "# CIS 194: Homework 10"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "3a4d88af-7f71-4a45-909c-918142094979",
   "metadata": {},
   "outputs": [],
   "source": [
    "import           Control.Applicative\n",
    "\n",
    "import           Data.Char\n",
    "\n",
    "-- A parser for a value of type a is a function which takes a String\n",
    "-- represnting the input to be parsed, and succeeds or fails; if it\n",
    "-- succeeds, it returns the parsed value along with the remainder of\n",
    "-- the input.\n",
    "newtype Parser a = Parser { runParser :: String -> Maybe (a, String) }\n",
    "\n",
    "-- For example, 'satisfy' takes a predicate on Char, and constructs a\n",
    "-- parser which succeeds only if it sees a Char that satisfies the\n",
    "-- predicate (which it then returns).  If it encounters a Char that\n",
    "-- does not satisfy the predicate (or an empty input), it fails.\n",
    "satisfy :: (Char -> Bool) -> Parser Char\n",
    "satisfy p = Parser f\n",
    "  where\n",
    "    f [] = Nothing    -- fail on the empty input\n",
    "    f (x:xs)          -- check if x satisfies the predicate\n",
    "                        -- if so, return x along with the remainder\n",
    "                        -- of the input (that is, xs)\n",
    "        | p x       = Just (x, xs)\n",
    "        | otherwise = Nothing  -- otherwise, fail\n",
    "\n",
    "-- Using satisfy, we can define the parser 'char c' which expects to\n",
    "-- see exactly the character c, and fails otherwise.\n",
    "char :: Char -> Parser Char\n",
    "char c = satisfy (== c)\n",
    "\n",
    "{- For example:\n",
    "\n",
    "*Parser> runParser (satisfy isUpper) \"ABC\"\n",
    "Just ('A',\"BC\")\n",
    "*Parser> runParser (satisfy isUpper) \"abc\"\n",
    "Nothing\n",
    "*Parser> runParser (char 'x') \"xyz\"\n",
    "Just ('x',\"yz\")\n",
    "\n",
    "-}\n",
    "\n",
    "-- For convenience, we've also provided a parser for positive\n",
    "-- integers.\n",
    "posInt :: Parser Integer\n",
    "posInt = Parser f\n",
    "  where\n",
    "    f xs\n",
    "      | null ns   = Nothing\n",
    "      | otherwise = Just (read ns, rest)\n",
    "      where (ns, rest) = span isDigit xs"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "2b6d20a6-b072-4aa6-b93a-68ffc8f7aeac",
   "metadata": {},
   "source": [
    "## Exercise 1\n",
    "\n",
    "First, you’ll need to implement a `Functor` instance for `Parser`.\n",
    "*Hint*: You may find it useful to implement a function\n",
    "\n",
    "```haskell\n",
    "first :: (a -> b) -> (a,c) -> (b,c)\n",
    "```"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "id": "973dd92d-459b-4f3b-b243-dae7fc8dd4dd",
   "metadata": {},
   "outputs": [],
   "source": [
    "first :: (a -> b) -> (a, c) -> (b, c)\n",
    "first f (a, c) = (f a, c)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "id": "4dfacc1a-e4ff-4649-ad80-681238e1af1b",
   "metadata": {},
   "outputs": [],
   "source": [
    "instance Functor Parser where\n",
    "  fmap :: (a -> b) -> Parser a -> Parser b\n",
    "  fmap f (Parser runA) = Parser $ (first f <$>) . runA"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "d3bd3d10-26ab-46ea-8ef7-30d705fe09a9",
   "metadata": {},
   "source": [
    "## Exercise 2\n",
    "\n",
    "Now implement an Applicative instance for `Parser`:\n",
    "\n",
    "- `pure` a represents the parser which consumes no input and successfully returns a result of `a`.\n",
    "- `p1 <*> p2` represents the parser which first runs `p1` (which will\n",
    "consume some input and produce a function), then passes the\n",
    "remaining input to `p2` (which consumes more input and produces\n",
    "some value), then returns the result of applying the function to the\n",
    "value. However, if either `p1` or `p2` fails then the whole thing should\n",
    "also fail (put another way, `p1 <*> p2` only succeeds if both `p1` and\n",
    "`p2` succeed)."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "id": "b22f4e86-d778-4df8-962e-78816acbb90b",
   "metadata": {},
   "outputs": [],
   "source": [
    "instance Functor Parser where\n",
    "  fmap :: (a -> b) -> Parser a -> Parser b\n",
    "  fmap f (Parser runA) = Parser $ (first f <$>) . runA"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "id": "0ec8cd46-05f9-4cad-87f4-988b5d90563a",
   "metadata": {},
   "outputs": [],
   "source": [
    "instance Applicative Parser where\n",
    "  pure a = Parser (\\s -> Just (a, s))\n",
    "  (Parser pf) <*> (Parser px) = Parser $ \\s -> do\n",
    "    (f, s') <- pf s\n",
    "    (x, s'') <- px s'\n",
    "    return (f x, s'')"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "8dc10832-1ed1-4503-a941-e141d6f4c889",
   "metadata": {},
   "source": [
    "So what is this good for? Recalling the Employee example from\n",
    "class,"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 6,
   "id": "07c046e5-1ed8-4799-8ce9-2f45704e2407",
   "metadata": {},
   "outputs": [],
   "source": [
    "type Name = String\n",
    "data Employee = Emp {name :: Name, phone :: String} deriving (Show)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "f496169d-2cf8-465a-87d7-8a2817814321",
   "metadata": {},
   "source": [
    "we could now use the Applicative instance for `Parser` to make an\n",
    "employee parser from name and phone parsers. That is, if\n",
    "```haskell\n",
    "parseName :: Parser Name\n",
    "parsePhone :: Parser String\n",
    "\n",
    "Emp <$> parseName <*> parsePhone :: Parser Employee\n",
    "```\n",
    "\n",
    "is a parser which first reads a name from the input, then a phone\n",
    "number, and returns them combined into an `Employee` record. Of\n",
    "course, this assumes that the name and phone number are right\n",
    "next to each other in the input, with no intervening separators. We’ll\n",
    "see later how to make parsers that can throw away extra stuff that\n",
    "doesn’t directly correspond to information you want to parse."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 7,
   "id": "20044569-5beb-4c4c-9129-61adb9136a5f",
   "metadata": {},
   "outputs": [],
   "source": [
    "runWordParser [] = Nothing\n",
    "runWordParser s = Just (x, unwords xs)\n",
    "  where\n",
    "    (x : xs) = words s\n",
    "\n",
    "parseName :: Parser Name\n",
    "parseName = Parser runWordParser\n",
    "\n",
    "parsePhone :: Parser String\n",
    "parsePhone = Parser runWordParser"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 8,
   "id": "180429da-08f8-4103-a55d-1e62b9af47cd",
   "metadata": {},
   "outputs": [],
   "source": [
    "pe = Emp <$> parseName <*> parsePhone :: Parser Employee"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 9,
   "id": "b2f3763e-22af-44cd-8b7b-eace8252d577",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "Just (Emp {name = \"alice\", phone = \"173\"},\"\")"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "runParser pe \"alice 173\""
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 10,
   "id": "2874c3ba-814d-4c1a-b319-fded53a02c53",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "Just (Emp {name = \"Bob\", phone = \"902\"},\"hs\")"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "runParser pe \"Bob 902 hs\""
   ]
  },
  {
   "cell_type": "markdown",
   "id": "f44eded6-94bc-4c9b-9512-eb5ee4d729ac",
   "metadata": {},
   "source": [
    "## Exercise 3\n",
    "\n",
    "We can also test your `Applicative` instance using other simple\n",
    "applications of functions to multiple parsers. You should implement\n",
    "each of the following exercises using the `Applicative` interface to put\n",
    "together simpler parsers into more complex ones. Do *not* implement\n",
    "them using the low-level definition of a `Parser`! In other words, pretend that you do not have access to the `Parser` constructor or even\n",
    "know how the Parser type is defined."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "6cc87a07-f47c-4578-8db3-e5e89cacc6d3",
   "metadata": {},
   "source": [
    "- Create a parser\n",
    "```haskell\n",
    "abParser :: Parser (Char, Char) \n",
    "```"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "aafb7e0b-2caa-4034-806c-257e90632e42",
   "metadata": {},
   "source": [
    "which expects to see the characters `’a’` and `’b’` and returns them\n",
    "as a pair. That is,\n",
    "```\n",
    "*AParser> runParser abParser \"abcdef\"\n",
    "Just ((’a’,’b’),\"cdef\")\n",
    "*AParser> runParser abParser \"aebcdf\"\n",
    "Nothing\n",
    "```"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 11,
   "id": "9d683bc1-3bf4-4c3a-bc21-193fd7611a24",
   "metadata": {},
   "outputs": [],
   "source": [
    "abParser :: Parser (Char, Char)\n",
    "abParser = (,) <$> char 'a' <*> char 'b'"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 12,
   "id": "2f71df2e-9f7b-4798-a6cb-de4e7c0fd361",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "Just (('a','b'),\"cdef\")"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "runParser abParser \"abcdef\""
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 13,
   "id": "e0ebcda5-be16-48ea-8b54-3ed7a22efd3a",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "Nothing"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "runParser abParser \"aebcdf\""
   ]
  },
  {
   "cell_type": "markdown",
   "id": "b38349a7-7199-43e6-86d9-0b667b4888f6",
   "metadata": {},
   "source": [
    "- Create a parser `intPair` which reads two integer values separated\n",
    "by a space and returns the integer values in a list. You should use\n",
    "the provided `posInt` to parse the integer values\n",
    "\n",
    "```\n",
    "*Parser> runParser intPair \"12 34\"\n",
    "Just ([12,34],\"\")\n",
    "```"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 14,
   "id": "e80c9fc9-acb1-41fb-95fa-49b335d34d5c",
   "metadata": {},
   "outputs": [],
   "source": [
    "intPair :: Parser [Integer]\n",
    "intPair = (\\x _ y -> [x, y]) <$> posInt <*> char ' ' <*> posInt"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 15,
   "id": "1ecca7d0-cbf5-4869-8c2f-c8c6c659ae47",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "Just ([12,34],\"\")"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "runParser intPair \"12 34\""
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 16,
   "id": "4f27ff7e-8183-4665-9403-39455725c030",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "Just (12,\" 34\")"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "runParser posInt \"12 34\""
   ]
  },
  {
   "cell_type": "markdown",
   "id": "a3de9d7b-9283-48c4-ae23-98b0af6ce54b",
   "metadata": {},
   "source": [
    "## Exercise 4\n",
    "\n",
    "`Applicative` by itself can be used to make parsers for simple, fixed\n",
    "formats. But for any format involving *choice* ( *e.g.* “. . . after the colon\n",
    "there can be a number **or** a word **or** parentheses. . . ”) `Applicative` is\n",
    "not quite enough. To handle choice we turn to the `Alternative` class,\n",
    "defined (essentially) as follows:\n",
    "\n",
    "```haskell\n",
    "class Applicative f => Alternative f where\n",
    "    empty :: f a\n",
    "    (<|>) :: f a -> f a -> f a\n",
    "```\n",
    "\n",
    "(<|>) is intended to represent choice: that is, f1 <|> f2 represents\n",
    "a choice between f1 and f2. `empty` should be the identity element for\n",
    "(<|>), and often represents *failure*."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "ef228be2-69c0-4a8c-b7fd-6a219a10d486",
   "metadata": {},
   "source": [
    "Write an `Alternative` instance for `Parser`:\n",
    "\n",
    "- `empty` represents the parser which always fails.\n",
    "- p1 <|> p2 represents the parser which first tries running p1. If\n",
    "p1 succeeds then p2 is ignored and the result of p1 is returned.\n",
    "Otherwise, if p1 fails, then p2 is tried instead.\n",
    "\n",
    "*Hint*: there is already an `Alternative` instance for `Maybe` which you\n",
    "may find useful."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 17,
   "id": "4e0464b9-9d67-4c55-a448-1dbd268ce365",
   "metadata": {},
   "outputs": [],
   "source": [
    "instance Alternative Parser where\n",
    "  empty = Parser $ const Nothing\n",
    "  (Parser r1) <|> (Parser r2) = Parser $ \\s -> r1 s <|> r2 s"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "c7f8429b-73c4-42db-b604-b1aaecca38e1",
   "metadata": {},
   "source": [
    "## Exercise 5\n",
    "\n",
    "Implement a parser\n",
    "```hasekll\n",
    "intOrUppercase :: Parser () \n",
    "```\n",
    "which parses either an integer value or an uppercase character, and\n",
    "fails otherwise.\n",
    "\n",
    "```\n",
    "*Parser> runParser intOrUppercase \"342abcd\"\n",
    "Just ((), \"abcd\")\n",
    "*Parser> runParser intOrUppercase \"XYZ\"\n",
    "Just ((), \"YZ\")\n",
    "*Parser> runParser intOrUppercase \"foo\"\n",
    "Nothing\n",
    "```"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 18,
   "id": "3d58dbf3-b78e-4261-832f-f0057b9499e7",
   "metadata": {},
   "outputs": [],
   "source": [
    "intOrUppercase :: Parser ()\n",
    "intOrUppercase = f posInt <|> f (satisfy isUpper)\n",
    "  where\n",
    "    f = (const () <$>)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 19,
   "id": "b56a7bd0-8fd4-469b-be68-0ef80a97ee0d",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "Just ((),\"abcd\")"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "runParser intOrUppercase \"342abcd\""
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 20,
   "id": "f0cecf93-cdbf-471b-8a0b-b098b8e74165",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "Just ((),\"YZ\")"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "runParser intOrUppercase \"XYZ\""
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 21,
   "id": "1a07dcc6-3447-46f2-a3da-9560b254faa3",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "Nothing"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "runParser intOrUppercase \"foo\""
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "fa2f87b1-58c9-4c82-a06d-50d97a1c7439",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Haskell",
   "language": "haskell",
   "name": "haskell"
  },
  "language_info": {
   "codemirror_mode": "ihaskell",
   "file_extension": ".hs",
   "mimetype": "text/x-haskell",
   "name": "haskell",
   "pygments_lexer": "Haskell",
   "version": "9.6.7"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
